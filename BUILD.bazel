load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library", "cc_test")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake")
load("@rules_pkg//:pkg.bzl", "pkg_deb", "pkg_tar")
load("@rules_pkg//:rpm.bzl", "pkg_rpm")

config_setting(
    name = "opt",
    values = {"compilation_mode": "opt"},
    visibility = ["//visibility:private"],
)

COPTS = [
    "-std=c++17",
    "-Werror",
    "-Wall",
    "-Wextra",
    "-Wpedantic",
    "-Wvla",
] + select({
    "//:opt": [
        "-Ofast",
        "-flto",
    ],
    "//conditions:default": [],
})

LINKOPTS = select({
    "//:opt": [
        "-O3",
        "-flto",
        "-s",
    ],
    "//conditions:default": [],
})

filegroup(
    name = "cmake_rules",
    srcs = [
        "CMakeLists.txt",
    ] + glob([
        "cmake/**/*",
    ]),
)

cmake(
    name = "buildinfo",
    cache_entries = {
        "BUILDINFO_ONLY": "ON",
        "USE_RUNTIME_CA_DETECTION": "ON",
    },
    lib_source = "//:cmake_rules",
    out_headers_only = True,
)

filegroup(
    name = "included_headers",
    srcs = glob(
        ["include/**/*.h", "include/**/*.hpp"],
        exclude = ["include/buildinfo.h"],
    ),
)

cc_library(
    name = "rtorrent_common",
    srcs = glob(
        ["src/**/*.cc"],
        exclude = ["src/main.cc"],
    ) + ["//:included_headers"],
    copts = COPTS,
    includes = ["include"],
    linkopts = LINKOPTS + select({
        "@platforms//os:macos": [
            "-lxml2",
            "-liconv",
            "-lncurses",
        ],
        "//conditions:default": [
            "-lpthread",
            "-lstdc++",
        ],
    }),
    deps = [
        "//:buildinfo",
        "@curl",
        "@json",
        "@libtorrent//:torrent",
    ] + select({
        "@platforms//os:macos": [],
        "//conditions:default": [
            "@ncurses//:ncursesw",
        ],
    }),
)

cc_binary(
    name = "rtorrent",
    srcs = [
        "src/main.cc",
        "@mimalloc",
    ],
    copts = COPTS,
    includes = ["include"],
    linkopts = LINKOPTS,
    linkstatic = True,
    deps = [
        "//:rtorrent_common",
    ],
)

cc_library(
    name = "test_common",
    srcs = ["test/main.cc"],
    copts = COPTS,
    includes = ["include"],
    linkopts = LINKOPTS,
    deps = [
        "//:rtorrent_common",
        "@com_google_googletest//:gtest",
    ],
)

[cc_test(
    name = t.split("/")[-1][:-3],
    srcs = [
        t,
        "@mimalloc",
    ],
    copts = COPTS,
    includes = ["include"],
    linkopts = LINKOPTS,
    tags = ["rtorrent_test"],
    deps = ["//:test_common"],
) for t in glob([
    "test/**/test_*.cc",
])]

pkg_tar(
    name = "rtorrent-bin",
    srcs = ["//:rtorrent"],
    mode = "0755",
    package_dir = "/usr/bin",
    strip_prefix = "/",
)

pkg_tar(
    name = "rtorrent-cfg",
    srcs = ["doc/rtorrent.rc"],
    mode = "0644",
    package_dir = "/etc/rtorrent",
    strip_prefix = "/doc",
)

pkg_tar(
    name = "rtorrent-service",
    srcs = ["doc/rtorrent@.service"],
    mode = "0644",
    package_dir = "/etc/systemd/system",
    strip_prefix = "/doc",
)

pkg_tar(
    name = "rtorrent-pkg-data",
    extension = "tar.gz",
    deps = [
        ":rtorrent-bin",
        ":rtorrent-cfg",
        ":rtorrent-service",
    ],
)

pkg_deb(
    name = "rtorrent-deb",
    architecture = "all",
    conffiles = [
        "/etc/rtorrent/rtorrent.rc",
        "/etc/systemd/system/rtorrent@.service",
    ],
    data = ":rtorrent-pkg-data",
    description = "a stable and high-performance BitTorrent client",
    homepage = "https://github.com/jesec/rtorrent",
    maintainer = "Jesse Chan <jc@linux.com>",
    package = "rtorrent",
    version = "0.9.8-jc+master",
)

pkg_rpm(
    name = "rtorrent-rpm",
    architecture = "all",
    data = [":rtorrent-pkg-data"],
    release = "100master.jc",
    spec_file = "doc/rtorrent.spec",
    version = "0.9.8",
)
